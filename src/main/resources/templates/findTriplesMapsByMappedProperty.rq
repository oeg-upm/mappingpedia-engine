#findTriplesMapsByMappedProperty.rq

prefix mpv: <http://mappingpedia.linkeddata.es/vocabulary#>
prefix rr: <http://www.w3.org/ns/r2rml#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix dcat: <http://www.w3.org/ns/dcat#>
prefix dcterms: <http://purl.org/dc/elements/1.1/>

SELECT DISTINCT ?md ?title ?dataset ?creator ?distribution ?distributionAccessURL ?mappingDocumentFile ?dateSubmitted
FROM <$graphURL>
WHERE {
    ?tm rr:predicateObjectMap ?pom .
    {
        ?pom rr:predicate ?searchedProperty .
        FILTER regex(str(?searchedProperty), "$mappedProperty", "i")
    }
    UNION
    {
        ?pom rr:predicateMap ?pm . ?pm rr:constant ?searchedProperty .
        FILTER regex(str(?searchedProperty), "$mappedProperty", "i")
    }

    { ?md a mpv:R2RMLMappingDocument . }
    UNION
    { ?md a mpv:MappingDocument . }

    ?md mpv:hasTriplesMaps ?tm .
    OPTIONAL { ?md mpv:mappingDocumentTitle ?title . }
    OPTIONAL { ?md mpv:hasMappingLanguage ?mappingLanguage . }
    OPTIONAL {
        ?md mpv:hasDataset ?dataset .
        ?dataset dcterms:identifier ?datasetId .
        ?dataset dcat:distribution ?distribution .
        ?distribution dcat:accessURL ?distributionAccessURL .
    }
    OPTIONAL { ?md mpv:hasMappingDocumentFile ?mappingDocumentFile . }
    OPTIONAL { ?md mpv:mappingDocumentCreator ?creator . }
    OPTIONAL { ?md mpv:mappingDocumentDateSubmitted ?dateSubmitted . }
}
ORDER BY ASC(?dateSubmitted)