prefix mpv: <http://mappingpedia.linkeddata.es/vocabulary#>
prefix rr: <http://www.w3.org/ns/r2rml#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix dcat: <http://www.w3.org/ns/dcat#>
prefix dcterms: <http://purl.org/dc/elements/1.1/>

SELECT DISTINCT *
FROM <$graphURL>
WHERE {
    ?tm rr:predicateObjectMap ?pom .
    ?pom rr:objectMap ?om .
    {
        ?om rr:column ?column .
        FILTER regex(str(?column), "$mappedColumn", "i")
    }
    UNION
    {
        ?om rr:template ?template.
        FILTER regex(str(?template), "$mappedColumn", "i")
    }

    { ?md a mpv:R2RMLMappingDocument . }
    UNION
    { ?md a mpv:MappingDocument . }

    ?md mpv:hasTriplesMaps ?tm .
    OPTIONAL { ?md mpv:mappingDocumentTitle ?title . }
    OPTIONAL {
        ?md mpv:hasDataset ?dataset .
        ?dataset dcterms:identifier ?datasetId .
        ?dataset dcat:distribution ?distribution .
        ?distribution dcat:accessURL ?distributionAccessURL .
    }
    OPTIONAL { ?md mpv:hasMappingDocumentFile ?mappingDocumentFile . }
    OPTIONAL { ?md mpv:mappingDocumentCreator ?creator . }
    OPTIONAL { ?md mpv:mappingDocumentDateSubmitted ?dateSubmitted . }
}
ORDER BY ASC(?dateSubmitted)